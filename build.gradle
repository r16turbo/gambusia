/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.4.1/userguide/java_library_plugin.html
 */

// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven'

group = 'io.gambusia'
version = '0.2.3-SNAPSHOT'

description = 'Netty-based MQTT v3.1.1 lightweight client'

tasks.withType(JavaCompile) {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

buildscript {
    repositories {
        // Use jcenter for resolving your dependencies.
        // You can declare any Maven/Ivy/file repository here.
        jcenter()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    }
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'io.netty:netty-codec-mqtt:4.1.13.Final'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    //implementation 'com.google.guava:guava:20'

    // Use JUnit test framework
    testImplementation 'org.apache.logging.log4j:log4j-api:2.10.0'
    testImplementation 'io.netty:netty-handler:4.1.13.Final'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.0.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.0.2'
    testCompileOnly 'org.apiguardian:apiguardian-api:1.0.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.0.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.0.2'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.10.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        customizePom(pom, project)
    }
}

uploadArchives {
    repositories.mavenDeployer {
        customizePom(pom, project)
        repository(url: "file:$rootDir/../maven")
    }
}

def customizePom(pom, gradleProject) {
    pom.whenConfigured { generatedPom ->
        // eliminate test-scoped dependencies
        generatedPom.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        generatedPom.project {
            name = gradleProject.name
            description = gradleProject.description
            url = "https://github.com/r16turbo/gambusia"
            licenses {
                license {
                    name "Apache License, Version 2.0"
                    url "http://www.apache.org/licenses/LICENSE-2.0"
                    distribution "repo"
                }
            }
            scm {
                url = "https://github.com/r16turbo/gambusia"
                connection = "scm:git:git://github.com/r16turbo/gambusia"
                developerConnection = "scm:git:git://github.com/r16turbo/gambusia"
            }
            developers {
                developer {
                    id = "r16turbo"
                    name = "Issey Yamakoshi"
                }
            }
            issueManagement {
                system = "GitHub Issues"
                url = "https://github.com/r16turbo/gambusia/issues"
            }
        }
    }
}
