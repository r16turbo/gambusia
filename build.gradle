/*
 * Copyright (C) 2018 Issey Yamakoshi
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'osgi'
}

ext {
    title = 'Gambusia/MQTT'

    nettyVersion   = '4.1.13.Final'
    log4jVersion   = '2.11.1'
    junitVersion   = '5.3.0'
    assertjVersion = '3.11.1'
}

group = 'io.gambusia'
version = '0.21.1-SNAPSHOT'
description = 'Netty-based MQTT v3.1.1 lightweight client'

dependencies {
    api("io.netty:netty-codec-mqtt:${nettyVersion}")

    testImplementation("org.apache.logging.log4j:log4j-api:${log4jVersion}")
    testImplementation("io.netty:netty-handler:${nettyVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testImplementation("org.assertj:assertj-core:${assertjVersion}")
    testRuntimeOnly("org.apache.logging.log4j:log4j-core:${log4jVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

repositories {
    jcenter()
}

tasks.withType(JavaCompile) {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        instruction 'Implementation-Title', title
        instruction 'Implementation-Version', version
        instruction 'Automatic-Module-Name', 'io.gambusia.mqtt'

        description project.description
        docURL 'https://github.com/r16turbo/gambusia'
        license 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
            artifact sourcesJar
            pom {
                name = title
                description = project.description
                url = 'https://github.com/r16turbo/gambusia'
                inceptionYear = '2018'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                scm {
                    url = 'https://github.com/r16turbo/gambusia'
                    connection = 'scm:git:https://github.com/r16turbo/gambusia.git'
                    developerConnection = 'scm:git:git@github.com:r16turbo/gambusia.git'
                }
                developers {
                    developer {
                        id = 'r16turbo'
                        name = 'Issey Yamakoshi'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/r16turbo/gambusia/issues'
                }
            }
        }
    }
    repositories {
        maven {
            url "file:$rootDir/../maven"
        }
    }
}
