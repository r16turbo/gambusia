/*
 * Copyright 2018 Issey Yamakoshi
 *
 * The Netty Project licenses this file to you under the Apache License,
 * version 2.0 (the "License"); you may not use this file except in compliance
 * with the License. You may obtain a copy of the License at:
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations
 * under the License.
 */

plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'maven-publish'
    id 'osgi'
}

ext {
    title = 'Gambusia/MQTT'
}

group = 'io.gambusia'
version = '0.9.3-SNAPSHOT'

description = 'Netty-based MQTT v3.1.1 lightweight client'

tasks.withType(JavaCompile) {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'io.netty:netty-codec-mqtt:4.1.13.Final'

    // Use JUnit 5 test framework
    testImplementation 'org.apache.logging.log4j:log4j-api:2.11.0'
    testImplementation 'io.netty:netty-handler:4.1.13.Final'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.1.0'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.11.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

test {
    useJUnitPlatform()
    exclude 'io/gambusia/**'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    manifest {
        instruction 'Implementation-Title', title
        instruction 'Implementation-Version', version
        instruction 'Implementation-Vendor', 'Issey Yamakoshi'
        instruction 'Automatic-Module-Name', 'io.gambusia.mqtt'

        description project.description
        docURL 'https://github.com/r16turbo/gambusia'
        license 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
            artifact sourcesJar
            pom {
                name = title
                description = project.description
                url = 'https://github.com/r16turbo/gambusia'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                scm {
                    url = 'https://github.com/r16turbo/gambusia'
                    connection = 'scm:git:https://github.com/r16turbo/gambusia.git'
                    developerConnection = 'scm:git:git@github.com:r16turbo/gambusia.git'
                }
                developers {
                    developer {
                        id = 'r16turbo'
                        name = 'Issey Yamakoshi'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/r16turbo/gambusia/issues'
                }
            }
        }
    }
    repositories {
        maven {
            url "file:$rootDir/../maven"
        }
    }
}
